import org.gradle.jvm.toolchain.JavaToolchainService

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'net.findmybook'
version = '0.0.2-SNAPSHOT'

def javaVersion = 25
def targetRelease = 25

springBoot {
    mainClass = 'net.findmybook.BookRecommendationEngineApplication'
}

// Use Java 25 toolchain for all compile/run tasks
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = targetRelease
}

def toolchains = project.extensions.getByType(JavaToolchainService)

tasks.withType(Test).configureEach {
    javaLauncher = toolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    jvmArgs += '--enable-preview'
    systemProperty 'io.netty.noUnsafe', 'true'
}

tasks.withType(JavaExec).configureEach {
    javaLauncher = toolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    jvmArgs += '--enable-preview'
    systemProperty 'io.netty.noUnsafe', 'true'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/milestone' }
    maven { url = 'https://repo.spring.io/snapshot' }
}

ext {
    set('springAiVersion', "1.0.0-M6")
    set('testcontainersVersion', "1.20.4")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Database
    implementation 'org.postgresql:postgresql'

    // Caching
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // AI
    implementation platform("org.springframework.ai:spring-ai-bom:${springAiVersion}")
    implementation 'org.springframework.ai:spring-ai-openai-spring-boot-starter'

    // Utilities
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'io.github.wimdeblauwe:htmx-spring-boot-thymeleaf:4.0.1'
    implementation 'software.amazon.awssdk:s3:2.29.35'
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'

    // Resilience - Needed for Bulkhead etc.
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
    implementation 'io.github.resilience4j:resilience4j-bulkhead:2.2.0'
    implementation 'io.github.resilience4j:resilience4j-micrometer:2.2.0'

    // Error Prone
    implementation 'com.google.errorprone:error_prone_annotations:2.36.0'

    // Lombok - Compatible with Java 25
    compileOnly 'org.projectlombok:lombok:1.18.40'
    annotationProcessor 'org.projectlombok:lombok:1.18.40'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:localstack'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.1"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// Ensure frontend assets are built before resources are processed
tasks.register('buildFrontendCss', Exec) {
    workingDir 'frontend'
    commandLine 'npm', 'run', 'build:css'
    // Don't fail if frontend dir doesn't exist yet during initial setup
    ignoreExitValue = true
}

processResources.dependsOn 'buildFrontendCss'
